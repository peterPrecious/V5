<%
  Dim oDbClone, oRs_New, oRs_Old, vHit

 
  '...used for sites like ERGP
  Sub sQuickClone (vCustId, vNewAcctId, vCustLevel, vTitle, vMaxUsers)
    Dim aUpd(), vOk
    vSql = "SELECT * FROM CUST WHERE Cust_Id = '" & vCustId & "'"

    Redim Preserve aUpd(1, 5)      '...populate the array with the fields to update and value unless equal to "[ignore]"

    aUpd (0, 0) = "Cust_Id"
    aUpd (1, 0) = Left(vCustId,4) & vNewAcctId    

    aUpd (0, 1) = "Cust_AcctId"
    aUpd (1, 1) = vNewAcctId 

    aUpd (0, 2) = "Cust_Added"
    aUpd (1, 2) = Now

    aUpd (0, 3) = "Cust_Title"
    aUpd (1, 3) = vTitle

    aUpd (0, 4) = "Cust_MaxUsers"
    aUpd (1, 4) = vMaxUsers

    aUpd (0, 5) = "Cust_ParentId"
    aUpd (1, 5) = fCustAcctId (vCustId)

    vOk = fCloneIt ("V5_Vubz", vSql, aUpd)

    If vOk = "ok" Then
      sAddInternalMemb vNewAcctId

      vMemb_Internal = 0
      vMemb_MyWorld  = 1
      vMemb_Manager  = 1   

      vMemb_No = 0 : vMemb_Id = Left(vCustId, 4) & "_SALES" : vMemb_Level = 4 : sAddMemb vNewAcctId

      If vCustLevel = 4 Then sSetupRepository vNewAcctId

    End If
  End Sub



  Function fCloneIt (vDb, vSql, aUpd)
    '...return err if there was some problem other then discovered below
    fCloneIt = "error"
    sOpenDbClone (vDb)
    Set oRs_Old = Server.CreateObject("ADODB.Recordset")
    Set oRs_Old.ActiveConnection = oDbClone
    oRs_Old.CursorType     = 3     '...adOpenStatic
    oRs_Old.CursorLocation = 3     '...adUseClient
    oRs_Old.LockType       = 3     '...adOptimistic
    oRs_Old.Open vSql              '...this reads the record we want to clone
    If Not oRs_Old.Eof Then
      Set oRs_New = oRs_Old.Clone    '...clone the record info 
      oRs_New.AddNew                 '...add a new record into the recordset

      '...go through all fields to determine what needs updating
      For i = 0 To oRs_Old.Fields.Count - 1

        '...ignore any key fields
        If oRs_Old.Fields(i).Properties("KEYCOLUMN").Value = False Or oRs_Old.Fields(i).Properties("ISAUTOINCREMENT").Value = False Then

          '...see if this field is one of the fields you want to update
          vHit = -1
          For j = 0 To Ubound(aUpd, 2)
            If aUpd(1, j) <> "[ignore]" Then
              If aUpd(0, j) = oRs_New(i).Name Then
                vHit = j
                Exit For
              End If
            End If
          Next
        
          '...if yes, update with value passed in
          If vHit > -1 Then
            oRs_New(i).Value = aUpd(1, vHit)
          '...if not then copy the value from the original record generated by the SQL statement
          Else        
            oRs_New(i).Value = oRs_Old(i).Value
          End If

        End If
      Next 
      '...update record
      oRs_New.Update                 
      Set oRs_New = Nothing
      '...return ok if record was cloned successfully
      fCloneIt = "ok"
    Else
      '...return eof if the clone record is not on file 
      fCloneIt = "eof"
    End If
    Set oRs_Old = Nothing
    sCloseDbClone
  End Function 


  Sub sOpenDbClone (vDb)
    Set oDbClone = Server.CreateObject("ADODB.Connection")
    oDbClone.ConnectionString = "Provider=SQLOLEDB.1;Password=" & svHostDbPwd & ";Persist Security Info=True;User ID=sa;Initial Catalog=" & vDb & ";Data Source=SQL01,1400"
    oDbClone.Open
  End Sub


  Sub sCloseDbClone
    oDbClone.Close
    Set oDbClone = Nothing
  End Sub
  
  
  '  ...create a temp table to play using this SQL
  Sub sCreateIt (vDb, vTbl)
    sOpenDbClone (vDb)  
    vSql = "USE V5_Vubz; DROP TABLE " & vTbl
    oDbClone.Execute(vSql)  
    vSql = "USE V5_Vubz; CREATE TABLE " & vTbl & " (pooh int IDENTITY(1,1) PRIMARY KEY, a varchar(50) NOT NULL, b varchar(50) NULL, c varchar(50) NULL, d varchar(50) NULL)"
    oDbClone.Execute(vSql)  
    vSql = "INSERT INTO " & vTbl & " (a, b, c, d) VALUES ('11', '12', '13', '14')"
    oDbClone.Execute(vSql)  
    sCloseDbClone
  End SUb
    
  
%>